program Project1;
 
const
  D = [' '];
  Da = ['а', 'я', 'о', 'ё', 'ы', 'и', 'э', 'е', 'у', 'ю'];
var
  S : String;
  Dw, Dra : set of Char;
  Ch : Char;
  i, Len : Integer;
  F : Boolean;
begin
  repeat
    Writeln('Задайте текст:');
    Readln(S);
 
    {Решение.}
    Len := Length(S); {Длина строки.}
    Dw := []; {Множество букв, которые входят в текущее слово.}
    Dra := []; {Множество гласных букв, которые входят в каждое слово.}
    F := True; {True - найдено первое слово, иначе - False.}
    for i := 1 to Len do
      if not (S[i] in D) then {Если символ принадлежит слову.}
      begin
        Dw := Dw + [S[i]]; {Добавляем букву во множество букв текущего слова.}
        if (i = Len) or (S[i + 1] in D) then {Отслеживаем конец слова.}
        begin
          if F then {Если текущее слово является первым словом в тексте.}
          begin
            Dra := Dw * Da; {Все гласные буквы текущего слова.}
            F := False;
          end
          else {Если текущее слово не первое в тексте (второе, третье и т. д.)}
            Dra := Dw * Da * Dra; {Гласные буквы текущего слова, которые входят в каждое из предыдущих слов.}
          if Dra = [] then {Если искомое множество уже пусто, то прерываем обработку.}
            Break;
          Dw := []; {Обнуление множества букв текущего слова.}
        end;
      end;
 
    {Ответ.}
    if Dra = [] then
      Writeln('Нет ни одной гласной буквы, которая бы входила в каждое слово.')
    else
    begin
      Writeln('Множество гласных букв, которые входят в каждое слово:');
      for Ch := 'а' to 'ё' do
        if Ch in Dra then
          Write(Ch, ' ');
      Writeln;
    end;
 
    Writeln('Повторить - Enter. Выход - любой символ + Enter.');
    Readln(S);
  until S <> '';
end.